// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License.

/*
**==============================================================================
**
** internal.edl:
**
**     This file declares internal ECALLs/OCALLs used by liboehost/liboecore.
**
**==============================================================================
*/

enclave {

    include "openenclave/bits/types.h"
    include "openenclave/internal/sgxtypes.h"

    trusted {

        public uint32_t oe_internal_get_sgx_report_ecall(
            [in, size=opt_params_size] const void* opt_params,
            size_t opt_params_size,
            [out] sgx_report_t* report);

        public uint32_t oe_internal_verify_report(
            [in, size=report_size] const void* report,
            size_t report_size);

        public uint32_t oe_internal_get_public_key(
            [in, count=1] const oe_asymmetric_key_params_t* key_params,
            [in, size=key_info_size] const void* key_info,
            size_t key_info_size,
            [out, size=key_buffer_size] void* key_buffer,
            size_t key_buffer_size,
            [out, count=1 ] size_t* key_buffer_size_out);

        public uint32_t oe_internal_get_public_key_by_policy(
            uint32_t seal_policy,
            [in, count=1] const oe_asymmetric_key_params_t* key_params,
            [out, size=key_buffer_size] void* key_buffer,
            size_t key_buffer_size,
            [out, count=1 ] size_t* key_buffer_size_out,
            [out, size=key_info_size] void* key_info,
            size_t key_info_size,
            [out, count=1 ] size_t* key_info_size_out);
    };

    untrusted {

        uint32_t oe_internal_get_qetarget_info(
            [out] sgx_target_info_t* target_info);

        uint32_t oe_internal_get_quote(
            [in] const sgx_report_t* sgx_report,
            [out, size=quote_size] void* quote,
            size_t quote_size,
            [out] size_t* quote_size_out);

        uint32_t oe_internal_get_qe_identify_info(
            [out, size=qe_id_info_size] void* qe_id_info,
            size_t qe_id_info_size,
            [out, count=1] size_t* qe_id_info_size_out,
            [out, size=issuer_chain_size] void* issuer_chain,
            size_t issuer_chain_size,
            [out, count=1] size_t* issuer_chain_size_out);

        uint32_t oe_internal_get_revocation_info(
            /* fmspc */
            [in] uint8_t fmspc[6],

            /* crl_urls */
            size_t num_crl_urls,
            [in, string] const char* crl_urls0,
            [in, string] const char* crl_urls1,
            [in, string] const char* crl_urls2,

            /* tcb_info */
            [out, size=tcb_info_size] void* tcb_info,
            size_t tcb_info_size,
            [out, count=1] size_t* tcb_info_size_out,

            /* tcb_issuer_chain */
            [out, size=tcb_issuer_chain_size] void* tcb_issuer_chain,
            size_t tcb_issuer_chain_size,
            [out, count=1] size_t* tcb_issuer_chain_size_out,

            /* crl */
            [out, size=crl0_size] void* crl0,
            size_t crl0_size,
            [out, count=1] size_t* crl0_size_out,
            [out, size=crl1_size] void* crl1,
            size_t crl1_size,
            [out, count=1] size_t* crl1_size_out,
            [out, size=crl2_size] void* crl2,
            size_t crl2_size,
            [out, count=1] size_t* crl2_size_out,

            /* crl_issuer_chain */
            [out, size=crl_issuer_chain0_size] void* crl_issuer_chain0,
            size_t crl_issuer_chain0_size,
            [out, count=1] size_t* crl_issuer_chain0_size_out,
            [out, size=crl_issuer_chain1_size] void* crl_issuer_chain1,
            size_t crl_issuer_chain1_size,
            [out, count=1] size_t* crl_issuer_chain1_size_out,
            [out, size=crl_issuer_chain2_size] void* crl_issuer_chain2,
            size_t crl_issuer_chain2_size,
            [out, count=1] size_t* crl_issuer_chain2_size_out);
    };
};
